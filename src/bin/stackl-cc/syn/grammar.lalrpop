use crate::syn::*;
use crate::tok::*;

use crate::diag::syn::Error as SyntaxError;

grammar;


// A.1.3 Identifiers

Identifier: Ident = IDENT => <>.kind.unwrap_ident();


// A.1.5 Constants

Constant: Const = CONST => <>.kind.unwrap_const();

EnumerationConstant: Ident = Identifier;


// A.1.6 String literals

StringLiteral: StrLit = STR_LIT => <>.kind.unwrap_str_lit();


// A.2.1 Expressions

PrimaryExpr: Expr = {
	Identifier => Expr::Ident(<>),
	Constant => Expr::Const(<>),
	StringLiteral => Expr::StrLit(<>),
	"(" <expr:Expr> ")" => Expr::Paren(Box::new(expr)),
};

PostfixExpr: Expr = {
	PrimaryExpr,
	<expr:PostfixExpr> "[" <arr:Expr> "]" => Expr::Unary(ExprUnary{
		op: UnOp::Postfix(Postfix::Array(Box::new(arr))),
		expr: Box::new(expr),
	}),
	<expr:PostfixExpr> "(" <args:ArgumentExprList?> ")" => Expr::Unary(ExprUnary{
		op: UnOp::Postfix(Postfix::ArgExprList(args.unwrap_or_default())),
		expr: Box::new(expr),
	}),
	<expr:PostfixExpr> "." <ident:Identifier> => Expr::Unary(ExprUnary{
		op: UnOp::Postfix(Postfix::Dot(ident)),
		expr: Box::new(expr),
	}),
	<expr:PostfixExpr> "->" <ident:Identifier> => Expr::Unary(ExprUnary{
		op: UnOp::Postfix(Postfix::Arrow(ident)),
		expr: Box::new(expr),
	}),
	<expr:PostfixExpr> "++" => Expr::Unary(ExprUnary{
		op: UnOp::Postfix(Postfix::Inc),
		expr: Box::new(expr),
	}),
	<expr:PostfixExpr> "--" => Expr::Unary(ExprUnary{
		op: UnOp::Postfix(Postfix::Dec),
		expr: Box::new(expr),
	}),
	"(" <ty:TypeName> ")" "{" <list:InitializerList> ","? "}" => Expr::CompoundLiteral(ty, list),
};

ArgumentExprList: Vec<Expr> = {
	AssignmentExpr => vec![<>],
	<mut list:ArgumentExprList> "," <expr:AssignmentExpr> => {
		list.push(expr);
		list
	},
};

UnaryExpr: Expr = {
	PostfixExpr,
	"++" <expr:UnaryExpr> => Expr::Unary(ExprUnary{
		op: UnOp::Inc,
		expr: Box::new(expr),
	}),
	"--" <expr:UnaryExpr> => Expr::Unary(ExprUnary{
		op: UnOp::Dec,
		expr: Box::new(expr),
	}),
	<prefix:UnaryOperator> <expr:CastExpr> => Expr::Unary(ExprUnary{
		op: prefix,
		expr: Box::new(expr),
	}),
	"sizeof" <expr:UnaryExpr> => Expr::Unary(ExprUnary{
		op: UnOp::Sizeof,
		expr: Box::new(expr),
	}),
	"sizeof" "(" <ty:TypeName> ")" => Expr::Sizeof(ty),
};

UnaryOperator: UnOp = {
	"&" => UnOp::Amp,
	"*" => UnOp::Star,
	"+" => UnOp::Plus,
	"-" => UnOp::Minus,
	"~" => UnOp::Comp,
	"!" => UnOp::Neg,
};

CastExpr: Expr = {
	UnaryExpr,
	"(" <ty:TypeName> ")" <expr:CastExpr> => Expr::Unary(ExprUnary{
		op: UnOp::Cast(ty),
		expr: Box::new(expr),
	}),
};

MultiplicativeExpr: Expr = {
	CastExpr,
	MultiplicativeExpr "*" CastExpr => todo!("mult-expr * cast-expression"),
	MultiplicativeExpr "/" CastExpr => todo!("mult-expr / cast-expression"),
	MultiplicativeExpr "%" CastExpr => todo!("mult-expr % cast-expression"),
};

AdditiveExpr: Expr = {
	MultiplicativeExpr,
	<lhs:AdditiveExpr> "+" <rhs:MultiplicativeExpr> => Expr::Binary(ExprBinary{
		left: Box::new(lhs),
		op: BinOp::Add,
		right: Box::new(rhs)
	}),
	<lhs:AdditiveExpr> "-" <rhs:MultiplicativeExpr> => Expr::Binary(ExprBinary{
		left: Box::new(lhs),
		op: BinOp::Sub,
		right: Box::new(rhs),
	}),
};

ShiftExpr: Expr = {
	AdditiveExpr,
	ShiftExpr "<<" AdditiveExpr => todo!(),
	ShiftExpr ">>" AdditiveExpr => todo!(),
};

RelationalExpr: Expr = {
	ShiftExpr,
	RelationalExpr "<" ShiftExpr => todo!(),
	RelationalExpr ">" ShiftExpr => todo!(),
	RelationalExpr "<=" ShiftExpr => todo!(),
	RelationalExpr ">=" ShiftExpr => todo!(),
};

EqualityExpr: Expr = {
	RelationalExpr,
	EqualityExpr "==" RelationalExpr => todo!(),
	EqualityExpr "!=" RelationalExpr => todo!(),
};

AndExpr: Expr = {
	EqualityExpr,
	AndExpr "&" EqualityExpr => todo!(),
};

ExclusiveOrExpr: Expr = {
	AndExpr,
	ExclusiveOrExpr "^" AndExpr => todo!(),
};

InclusiveOrExpr: Expr = {
	ExclusiveOrExpr,
	InclusiveOrExpr "|" ExclusiveOrExpr => todo!(),
};

LogicalAndExpr: Expr = {
	InclusiveOrExpr,
	LogicalAndExpr "&&" InclusiveOrExpr => todo!(),
};

LogicalOrExpr: Expr = {
	LogicalAndExpr,
	LogicalOrExpr "||" LogicalAndExpr => todo!(),
};

ConditionalExpr: Expr = {
	LogicalOrExpr,
	LogicalOrExpr "?" Expr ":" ConditionalExpr => todo!(),
};

AssignmentExpr: Expr = {
	ConditionalExpr,
	<lhs:UnaryExpr> <op:AssignmentOperator> <rhs:AssignmentExpr> => {
		Expr::Binary(ExprBinary{
			left: Box::new(lhs),
			op: op,
			right: Box::new(rhs),
		})
	},
};

AssignmentOperator: BinOp = {
	"=" => BinOp::Assign,
	"*=" => BinOp::MulAssign,
	"/=" => BinOp::DivAssign,
	"%=" => BinOp::ModAssign,
	"+=" => BinOp::AddAssign,
	"-=" => BinOp::SubAssign,
	"<<=" => BinOp::LShiftAssign,
	">>=" => BinOp::RShiftAssign,
	"&=" => BinOp::AmpAssign,
	"^=" => BinOp::XOrAssign,
	"|=" => BinOp::OrAssign,
};

Expr: Expr = {
	AssignmentExpr => <>,
	<lhs:Expr> "," <rhs:AssignmentExpr> => Expr::Binary(ExprBinary{
		left: Box::new(lhs),
		op: BinOp::Comma,
		right: Box::new(rhs),
	}),
};

ConstantExpr = ConditionalExpr;



// A.2.2 Declarations

Declaration: Declaration = {
	<specs:DeclarationSpecifiers> <init_list:InitDeclaratorList?> ";" => Declaration {
		declaration_specifiers: specs,
		init_declarator_list: init_list.unwrap_or_default(),
	},
};

DeclarationSpecifiers: Vec<DeclarationSpecifier> = DeclarationSpecifier+;

DeclarationSpecifier: DeclarationSpecifier = {
	StorageClassSpecifier => DeclarationSpecifier::StorageClassSpecifier(<>),
	TypeSpecifier => DeclarationSpecifier::TypeSpecifier(<>),
	TypeQualifier => DeclarationSpecifier::TypeQualifier(<>),
	FunctionSpecifier => DeclarationSpecifier::FunctionSpecifier(<>),
};

InitDeclaratorList: Vec<InitDeclarator> = {
	InitDeclarator => vec![<>],
	<mut lhs:InitDeclaratorList> "," <rhs:InitDeclarator> => {
		lhs.push(rhs);
		lhs
	},
};

InitDeclarator: InitDeclarator = {
	Declarator => InitDeclarator {
		declarator: <>,
		initializer: None,
	},
	<decl:Declarator> "=" <init:Initializer> => InitDeclarator {
		declarator: decl,
		initializer: Some(init)
	},
};

StorageClassSpecifier: StorageClassSpecifier = {
	"typedef" => StorageClassSpecifier::Typedef,
	"extern" => StorageClassSpecifier::Extern,
	"static" => StorageClassSpecifier::Static,
	"auto" => StorageClassSpecifier::Auto,
	"register" => StorageClassSpecifier::Register,
}

TypeSpecifier: TypeSpecifier = {
	"void" => TypeSpecifier::Void,
	"char" => TypeSpecifier::Char,
	"short" => TypeSpecifier::Short,
	"int" => TypeSpecifier::Int,
	"long" => TypeSpecifier::Long,
	"float" => TypeSpecifier::Float,
	"double" => TypeSpecifier::Double,
	"signed" => TypeSpecifier::Signed,
	"unsigned" => TypeSpecifier::Unsigned,
	"_Bool" => TypeSpecifier::Bool,
	StructOrUnionSpecifier => TypeSpecifier::StructOrUnionSpecifier(<>),
	EnumSpecifier => TypeSpecifier::EnumSpecifier(<>),
	TypedefName => todo!("typedef-name"),
};

StructOrUnionSpecifier: StructOrUnionSpecifier = {
	StructOrUnion Identifier? "{" StructDeclarationList "}" => todo!(),
	StructOrUnion Identifier => todo!(),
};

StructOrUnion: Keyword = {
	"struct" => todo!("struct"),
	"union" => todo!("union"),
};

StructDeclarationList: StructDeclarationList = StructDeclaration+ => StructDeclarationList(<>);

StructDeclaration: StructDeclaration = {
	<list0:SpecifierQualifierList> <list1:StructDeclaratorList> ";" => StructDeclaration {
		specifier_qualifier_list: list0,
		struct_declaration_list: list1,
	}
};

SpecifierQualifierList: Vec<SpecifierQualifier> = SpecifierQualifier+;

SpecifierQualifier: SpecifierQualifier = {
	TypeSpecifier => SpecifierQualifier::TypeSpecifier(<>),
	TypeQualifier => SpecifierQualifier::TypeQualifier(<>),
};

StructDeclaratorList: Vec<StructDeclarator> = {
	StructDeclarator => vec![<>],
	<mut list:StructDeclaratorList> "," <elem:StructDeclarator> => {
		list.push(elem);
		list
	},
};

StructDeclarator: StructDeclarator = {
	<decl:Declarator> => StructDeclarator {
		declarator: Some(decl),
		constant_expr: None,
	},
	<decl:Declarator?> ":" <expr:ConstantExpr> => StructDeclarator {
		declarator: decl,
		constant_expr: Some(expr),
	},
};

EnumSpecifier: EnumSpecifier = {
	"enum" <id:Identifier?> "{" <list:EnumeratorList> ","? "}" => {
		EnumSpecifier {
			identifier: id,
			enumerator_list: list,
		}
	},
	"enum" <id:Identifier> => EnumSpecifier{
		identifier: Some(id),
		enumerator_list: vec![],
	},
};

EnumeratorList: Vec<Enumerator> = {
	Enumerator => vec![<>],
	<mut list:EnumeratorList> "," <elem:Enumerator> => {
		list.push(elem);
		list
	},
};

Enumerator: Enumerator = {
	<ident:EnumerationConstant> => Enumerator {
		enumeration_constant: ident,
		constant_expr: None,
	},
	<ident:EnumerationConstant> "=" <expr:ConstantExpr> => Enumerator {
		enumeration_constant: ident,
		constant_expr: Some(expr),
	},
};

TypeQualifier: TypeQualifier = {
	"const" => TypeQualifier::Const,
	"restrict" => TypeQualifier::Restrict,
	"volatile" => TypeQualifier::Volatile,
};

FunctionSpecifier: FunctionSpecifier = "inline" => FunctionSpecifier::Inline;

Declarator: Declarator = {
	<ptr:Pointer?> <dd_list:DirectDeclaratorList> => Declarator {
		pointer: ptr.unwrap_or_default(),
		direct_declarator: dd_list,
	},
};

DirectDeclaratorList: Vec<DirectDeclarator> = {
	Identifier => vec![DirectDeclarator::Identifier(<>)],
	"(" Declarator ")" => todo!(" ( declarator )"),
	DirectDeclaratorList "[" TypeQualifier* AssignmentExpr? "]" => todo!("direct type"),
	DirectDeclaratorList "[" "static" TypeQualifier* AssignmentExpr "]" => todo!("direct static"),
	DirectDeclaratorList "[" TypeQualifier+ "static" AssignmentExpr "]" => todo!("direct typeq"),
	DirectDeclaratorList "[" TypeQualifier* "*" "]" => todo!("direct typeq *"),
	<mut dd:DirectDeclaratorList> "(" <ptl: ParameterTypeList> ")" => {
		dd.push(DirectDeclarator::ParameterTypeList(ptl));
		dd
	},
	<mut dd:DirectDeclaratorList> "(" <ident_list:IdentifierList?> ")" => {
		dd.push(DirectDeclarator::IdentifierList(ident_list.unwrap_or_default()));
		dd
	},
};

Pointer: Vec<Pointer> = {
	"*" <tq:TypeQualifier*> => vec![Pointer {
		type_qualifier_list: tq,
	}],
	"*" TypeQualifier* Pointer => todo!("* type-qualifier pointer"),
};
ParameterTypeList: ParameterTypeList = {
	ParameterList => {
		ParameterTypeList{
			parameter_list: <>,
			comma_ellipsis: false,
		}
	},
	ParameterList "," "..." => todo!("parameter-list , ..."),
};

ParameterList: Vec<ParameterDeclaration> = {
	ParameterDeclaration => vec![<>],
	ParameterList "," ParameterDeclaration => todo!("parameter-list , parameter-declaration"),
};

ParameterDeclaration: ParameterDeclaration = {
	DeclarationSpecifiers Declarator => todo!("decalration-specifiers"),
	<decl_specs:DeclarationSpecifiers> <abstract_decl:AbstractDeclarator?> => {
		ParameterDeclaration::AbstractDeclarator {
			declaration_specifiers: decl_specs,
			abstract_declarator: abstract_decl,
		}
	},
};

IdentifierList: Vec<Ident> = {
	Identifier => vec![<>],
	IdentifierList "," Identifier => todo!("ident , ident"),
};

TypeName: TypeName = {
	SpecifierQualifierList AbstractDeclarator? => todo!("specifier-qualifier-list abstract-declarator"),
};

AbstractDeclarator: AbstractDeclarator = {
	Pointer => todo!("ptr"),
	Pointer? DirectAbstractDeclarator => todo!("ptr direct"),
};


DirectAbstractDeclarator: DirectAbstractDeclarator = {
	"(" AbstractDeclarator ")" => todo!("abstract-decl"),
	DirectAbstractDeclarator? "[" AssignmentExpr? "]" => todo!("direct assign"),
	DirectAbstractDeclarator? "[" "*" "]" => todo!("direct *"),
	DirectAbstractDeclarator? "(" ParameterTypeList? ")" => todo!("direct param"),
};


TypedefName: Ident = TYPE_NAME => todo!("typedef-name");

Initializer: Initializer = {
	AssignmentExpr => Initializer::Expr(<>),
	"{" InitializerList ","? "}" => todo!("init-list"),
};

InitializerList: InitializerList = {
	Designation? Initializer => todo!("desig init"),
	InitializerList "," Designation? Initializer => todo!("init list , desig"),
};

Designation: Designation = {
	Designator+ "=" => todo!("desig list"),
};

Designator: Designator = {
	"[" ConstantExpr "]" => todo!("const-expr"),
	"." Identifier => todo!(" . identifier"),
};


// A.2.3 Statements

Statement: Statement = {
	MatchedStatement,
	UnmatchedStatement,
};

MatchedStatement: Statement = {
	"if" "(" Expr ")" MatchedStatement "else" MatchedStatement => todo!(),
	"switch" "(" Expr ")" MatchedStatement => todo!(),
	MatchedLabeledStatement => todo!(),
	MatchedCompoundStatement => todo!(),
	ExprStatement => todo!(),
	IterationMatchedStatement => todo!(),
	JumpStatement => todo!(),
};

UnmatchedStatement: Statement = {
	"if" "(" Expr ")" UnmatchedStatement => todo!(),
	"if" "(" Expr ")" MatchedStatement "else" UnmatchedStatement => todo!(),
	"switch" "(" Expr ")" UnmatchedStatement => todo!(),
	UnmatchedLabeledStatement => todo!("unmatched-label"),
	UnmatchedCompoundStatement => todo!("unmatched-compound"),
	ExprStatement => todo!("expr-stmt"),
	IterationUnmatchedStatement => todo!("iter"),
	JumpStatement => todo!("jump"),
};

MatchedLabeledStatement: LabeledStatement = {
	Identifier ":" MatchedStatement => todo!("label"),
	"case" ConstantExpr ":" MatchedStatement => todo!("case"),
	"default" ":" MatchedStatement => todo!("default"),
};
UnmatchedLabeledStatement: LabeledStatement = {
	Identifier ":" UnmatchedStatement => todo!("label"),
	"case" ConstantExpr ":" UnmatchedStatement => todo!("case"),
	"default" ":" UnmatchedStatement => todo!("default"),
};


MatchedCompoundStatement: CompoundStatement = {
	"{" <blocks:MatchedBlockItem*> "}" => CompoundStatement(blocks),
};

UnmatchedCompoundStatement: CompoundStatement = {
	"{" UnmatchedBlockItem* "}" => todo!("compound"),
};

MatchedBlockItem: BlockItem = {
	Declaration => BlockItem::Declaration(<>),
	MatchedStatement => todo!("matched-stmt"),
};
UnmatchedBlockItem: BlockItem = {
	Declaration => todo!("decl"),
	UnmatchedStatement => todo!("unmatched-stmt"),
};

ExprStatement: ExprStatement = {
	Expr? ";" => todo!("expr"),
};

IterationMatchedStatement: IterationStatement = {
	"while" "(" Expr ")" MatchedStatement => todo!(),
	"do" MatchedStatement "while" "(" Expr ")" ";"
	"for" "(" Expr? ";" Expr? ";" Expr? ")" MatchedStatement => todo!(),
	"for" "(" Declaration Expr? ";" Expr? ")" MatchedStatement => todo!(),
};
IterationUnmatchedStatement: IterationStatement = {
	"while" "(" Expr ")" UnmatchedStatement => todo!(),
	"do" UnmatchedStatement "while" "(" Expr ")" ";"
	"for" "(" Expr? ";" Expr? ";" Expr? ")" UnmatchedStatement => todo!(),
	"for" "(" Declaration Expr? ";" Expr? ")" UnmatchedStatement => todo!(),
};

JumpStatement: JumpStatement = {
	"goto" Identifier ";" => todo!("goto"),
	"continue" ";" => todo!("continue"),
	"break" ";" => todo!("break"),
	"return" Expr? ";" => todo!("return"),
};

// A.2.4 External definitions

pub Syntax: TranslationUnit = ExternalDeclaration+ => {
	TranslationUnit {
		external_declaration: <>,
	}
};

ExternalDeclaration: ExternalDeclaration = {
	FunctionDefinition => ExternalDeclaration::FunctionDefinition(<>),
	Declaration => ExternalDeclaration::Declaration(<>),
};

FunctionDefinition: FunctionDefinition = {
	<specs: DeclarationSpecifiers> <decl:Declarator> <list:Declaration*> <compound:MatchedCompoundStatement> => {
		FunctionDefinition {
			declaration_specifiers: specs,
			declarator: decl,
			declaration_list: list,
			compound_statement: compound,
		}
	},
};

//DeclarationList: Vec<Declaration> = Declaration+;

extern {
	type Location = usize;
	type Error = SyntaxError;

	enum Token {
		IDENT => Token{kind: TokenKind::Ident(Ident{is_type:false, ..}), ..},
		TYPE_NAME => Token{kind: TokenKind::Ident(Ident{is_type:true, ..}), ..},
		CONST => Token{kind: TokenKind::Const(_), ..},
		STR_LIT => Token{kind: TokenKind::StrLit(_), ..},

		"typedef" => Token{kind: TokenKind::Keyword(Keyword::Typedef), ..},
		"extern" => Token{kind: TokenKind::Keyword(Keyword::Extern), ..},
		"static" => Token{kind: TokenKind::Keyword(Keyword::Static), ..},
		"auto" => Token{kind: TokenKind::Keyword(Keyword::Auto), ..},
		"register" => Token{kind: TokenKind::Keyword(Keyword::Register), ..},
		"void" => Token{kind: TokenKind::Keyword(Keyword::Void), ..},
		"char" => Token{kind: TokenKind::Keyword(Keyword::Char), ..},
		"short" => Token{kind: TokenKind::Keyword(Keyword::Short), ..},
		"int" => Token{kind: TokenKind::Keyword(Keyword::Int), ..},
		"long" => Token{kind: TokenKind::Keyword(Keyword::Long), ..},
		"float" => Token{kind: TokenKind::Keyword(Keyword::Float), ..},
		"double" => Token{kind: TokenKind::Keyword(Keyword::Double), ..},
		"signed" => Token{kind: TokenKind::Keyword(Keyword::Signed), ..},
		"unsigned" => Token{kind: TokenKind::Keyword(Keyword::Unsigned), ..},
		"_Bool" => Token{kind: TokenKind::Keyword(Keyword::Bool), ..},
		"const" => Token{kind: TokenKind::Keyword(Keyword::Const), ..},
		"restrict" => Token{kind: TokenKind::Keyword(Keyword::Restrict), ..},
		"volatile" => Token{kind: TokenKind::Keyword(Keyword::Volatile), ..},
		"enum" => Token{kind: TokenKind::Keyword(Keyword::Enum), ..},
		"struct" => Token{kind: TokenKind::Keyword(Keyword::Struct), ..},
		"sizeof" => Token{kind: TokenKind::Keyword(Keyword::Sizeof), ..},
		"union" => Token{kind: TokenKind::Keyword(Keyword::Union), ..},
		"inline" => Token{kind: TokenKind::Keyword(Keyword::Inline), ..},
		"case" => Token{kind: TokenKind::Keyword(Keyword::Case), ..},
		"default" => Token{kind: TokenKind::Keyword(Keyword::Default), ..},
		"if" => Token{kind: TokenKind::Keyword(Keyword::If), ..},
		"switch" => Token{kind: TokenKind::Keyword(Keyword::Switch), ..},
		"while" => Token{kind: TokenKind::Keyword(Keyword::While), ..},
		"do" => Token{kind: TokenKind::Keyword(Keyword::Do), ..},
		"for" => Token{kind: TokenKind::Keyword(Keyword::For), ..},
		"goto" => Token{kind: TokenKind::Keyword(Keyword::Goto), ..},
		"continue" => Token{kind: TokenKind::Keyword(Keyword::Continue), ..},
		"break" => Token{kind: TokenKind::Keyword(Keyword::Break), ..},
		"return" => Token{kind: TokenKind::Keyword(Keyword::Return), ..},
		"else" => Token{kind: TokenKind::Keyword(Keyword::Else), ..},

		"." => Token{kind: TokenKind::Punct(Punct::Dot), ..},
		"..." => Token{kind: TokenKind::Punct(Punct::Ellipsis), ..},
		"," => Token{kind: TokenKind::Punct(Punct::Comma), ..},
		"=" => Token{kind: TokenKind::Punct(Punct::Equal), ..},
		"*" => Token{kind: TokenKind::Punct(Punct::Star), ..},
		"+" => Token{kind: TokenKind::Punct(Punct::Plus), ..},
		"-" => Token{kind: TokenKind::Punct(Punct::Minus), ..},
		"~" => Token{kind: TokenKind::Punct(Punct::Tilde), ..},
		";" => Token{kind: TokenKind::Punct(Punct::SemiColon), ..},
		"{" => Token{kind: TokenKind::Punct(Punct::LCurly), ..},
		"}" => Token{kind: TokenKind::Punct(Punct::RCurly), ..},
		"(" => Token{kind: TokenKind::Punct(Punct::LParen), ..},
		")" => Token{kind: TokenKind::Punct(Punct::RParen), ..},
		")" => Token{kind: TokenKind::Punct(Punct::RParen), ..},
		"[" => Token{kind: TokenKind::Punct(Punct::LSquare), ..},
		"]" => Token{kind: TokenKind::Punct(Punct::RSquare), ..},
		"->" => Token{kind: TokenKind::Punct(Punct::Arrow), ..},
		"++" => Token{kind: TokenKind::Punct(Punct::PlusPlus), ..},
		"--" => Token{kind: TokenKind::Punct(Punct::MinusMinus), ..},
		"&" => Token{kind: TokenKind::Punct(Punct::Amp), ..},
		"!" => Token{kind: TokenKind::Punct(Punct::Bang), ..},
		"/" => Token{kind: TokenKind::Punct(Punct::FSlash), ..},
		"%" => Token{kind: TokenKind::Punct(Punct::Percent), ..},
		"<<" => Token{kind: TokenKind::Punct(Punct::LessLess), ..},
		">>" => Token{kind: TokenKind::Punct(Punct::GreatGreat), ..},
		"<" => Token{kind: TokenKind::Punct(Punct::Less), ..},
		">" => Token{kind: TokenKind::Punct(Punct::Amp), ..},
		"<=" => Token{kind: TokenKind::Punct(Punct::LessEqual), ..},
		">=" => Token{kind: TokenKind::Punct(Punct::GreatEqual), ..},
		"==" => Token{kind: TokenKind::Punct(Punct::EqualEqual), ..},
		"!=" => Token{kind: TokenKind::Punct(Punct::BangEqual), ..},
		"^" => Token{kind: TokenKind::Punct(Punct::Caret), ..},
		"|" => Token{kind: TokenKind::Punct(Punct::VBar), ..},
		"&&" => Token{kind: TokenKind::Punct(Punct::AmpAmp), ..},
		"||" => Token{kind: TokenKind::Punct(Punct::VBarVBar), ..},
		"?" => Token{kind: TokenKind::Punct(Punct::QMark), ..},
		":" => Token{kind: TokenKind::Punct(Punct::Colon), ..},
		"*=" => Token{kind: TokenKind::Punct(Punct::StarEqual), ..},
		"/=" => Token{kind: TokenKind::Punct(Punct::FSlashEqual), ..},
		"%=" => Token{kind: TokenKind::Punct(Punct::PercentEqual), ..},
		"+=" => Token{kind: TokenKind::Punct(Punct::PlusEqual), ..},
		"-=" => Token{kind: TokenKind::Punct(Punct::MinusEqual), ..},
		"<<=" => Token{kind: TokenKind::Punct(Punct::LessLessEqual), ..},
		">>=" => Token{kind: TokenKind::Punct(Punct::GreatGreatEqual), ..},
		"&=" => Token{kind: TokenKind::Punct(Punct::AmpEqual), ..},
		"^=" => Token{kind: TokenKind::Punct(Punct::CaretEqual), ..},
		"|=" => Token{kind: TokenKind::Punct(Punct::VBarEqual), ..},
	}
}
